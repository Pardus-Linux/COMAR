#!/usr/bin/python

import os
import sys
from distutils.sysconfig import get_config_var as pyconf

# Parameters

version = ARGUMENTS.get('version', '1.0')
prefix = ARGUMENTS.get('prefix', 'install')

# Source Distribution

distfiles = """
    SConstruct
    README
    model.xml
    src/*.c
    src/SConscript
    include/*.h
    bindings/SConscript
    bindings/comar.py
    bindings/*.c
    bindings/*.h
"""

if 'dist' in sys.argv:
    os.popen("tar -czf %s %s" % ("comar-" + version + ".tar.gz", " ".join(Split(distfiles))))
    Exit(0)

# Configuration

if sys.version_info[1] == 3:
    libs = [ 'db', 'm', "python2.3" ]
else:
    libs = [ 'db', 'm', "python2.4" ]

libpath = [ '/usr/lib' ]
cpppath = [ pyconf('CONFINCLUDEPY'), "../include" ]
ccflags = "-O2 -Wall -DVERSION='\"%s\"' " % version

env = Environment(CCFLAGS=ccflags, CPPPATH=cpppath, LIBS=libs, LIBPATH=libpath)

# Build

env.SConsignFile("sconsign")
Export('env')
comar = SConscript('src/SConscript', build_dir='build', duplicate=0)
libcomar, hav = SConscript('bindings/SConscript', build_dir='build/bindings', duplicate=0)

# Install

bindir = os.path.join(prefix, 'usr/bin')
libdir = os.path.join(prefix, 'usr/lib')
incdir = os.path.join(prefix, 'usr/include')
etcdir = os.path.join(prefix, 'etc/comar')
initdir = os.path.join(prefix, 'etc/init.d')
pydir = os.path.join(prefix, 'usr/lib/python2.3/site-packages')

targets = []
targets.append(env.Install(bindir, comar))
targets.append(env.Install(etcdir, 'model.xml'))
targets.append(env.Install(incdir, 'bindings/libcomar.h'))
targets.append(env.Install(libdir, libcomar))
targets.append(env.Install(pydir, 'bindings/comar.py'))
targets.append(env.Install(bindir, hav))
targets.append(env.Install(initdir, 'comard'))

env.Alias('install', targets)
